plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.palantir.docker' version '0.35.0'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.0'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.0'
}

allOpen {
    annotation("javax.persistence.Entity")
}

group = 'org.lucycato.event-query-service'
version = '1.0.0'

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

clean {
    delete file('src/main/generated')
}

ext {
    set('springCloudVersion', "2023.0.2")
}

bootRun {
    sourceResources sourceSets.main
}

dependencies {
    implementation project(':common')
    implementation project(':webflux')

    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += '-Xjsr305=strict'
        jvmTarget = '17'
    }
}

bootJar {
    enabled = true
}

jar {
    enabled = false
}

docker {
    def dockerHubName = 'lucycato'
    println(tasks.bootJar.outputs.files)

    name dockerHubName + '/' + rootProject.name + '-' + project.name + ':' + version

    dockerfile file('../Dockerfile')

    files tasks.bootJar.outputs.files

    buildArgs(['JAR_FILE': tasks.bootJar.outputs.files.singleFile.name])
}